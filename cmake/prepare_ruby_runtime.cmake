# Modified 2026 by Felix Valentin Herwig (sixdotsIT) for Klangten.
if(NOT PLATFORM OR NOT ARCH OR NOT RUBY_VERSION OR NOT RUNTIME_ROOT OR NOT PROJECT_ROOT)
  message(FATAL_ERROR "prepare_ruby_runtime.cmake requires PLATFORM, ARCH, RUBY_VERSION, RUNTIME_ROOT and PROJECT_ROOT")
endif()
if(NOT BUNDLE_ROOT)
  set(BUNDLE_ROOT "${RUNTIME_ROOT}/bundle")
endif()
if(NOT RUNTIME_STAMP)
  set(RUNTIME_STAMP "${RUNTIME_ROOT}/elten-ruby-runtime.stamp")
endif()

set(marker "${RUNTIME_ROOT}/.elten-ruby-runtime.version")
set(desired_marker "platform=${PLATFORM}\narch=${ARCH}\nversion=${RUBY_VERSION}\n")
set(desired_marker "${desired_marker}install_gems=${INSTALL_GEMS}\n")
if(PLATFORM STREQUAL "windows")
  set(desired_marker "${desired_marker}url=${WINDOWS_RUBY_URL}\n")
  set(desired_marker "${desired_marker}msys2_url=${MSYS2_URL}\n")
  set(desired_marker "${desired_marker}msys2_packages=${MSYS2_PACKAGES}\n")
  set(desired_marker "${desired_marker}nokogiri_msys_patch=${NOKOGIRI_MSYS_PATCH}\n")
  set(desired_marker "${desired_marker}nokogiri_build_options=${NOKOGIRI_BUILD_OPTIONS}\n")
elseif(PLATFORM STREQUAL "osx")
  set(desired_marker "${desired_marker}osx_configure_options=${OSX_RUBY_CONFIGURE_OPTIONS}\n")
elseif(PLATFORM STREQUAL "linux")
  set(desired_marker "${desired_marker}url=${LINUX_RUBY_URL}\n")
  set(desired_marker "${desired_marker}linux_configure_options=${LINUX_RUBY_CONFIGURE_OPTIONS}\n")
endif()

set(rubyinstaller_arch "${ARCH}")
if(PLATFORM STREQUAL "windows" AND ARCH STREQUAL "arm64")
  set(rubyinstaller_arch "arm")
endif()

function(run_checked)
  execute_process(
    COMMAND ${ARGV}
    RESULT_VARIABLE result
  )
  if(NOT result EQUAL 0)
    string(REPLACE ";" " " rendered "${ARGV}")
    message(FATAL_ERROR "Command failed (${result}): ${rendered}")
  endif()
endfunction()

function(run_checked_in working_directory)
  execute_process(
    COMMAND ${ARGN}
    WORKING_DIRECTORY "${working_directory}"
    RESULT_VARIABLE result
  )
  if(NOT result EQUAL 0)
    string(REPLACE ";" " " rendered "${ARGN}")
    message(FATAL_ERROR "Command failed (${result}) in ${working_directory}: ${rendered}")
  endif()
endfunction()

function(run_checked_with_c_locale)
  set(old_lang "$ENV{LANG}")
  set(old_lc_all "$ENV{LC_ALL}")
  set(old_language "$ENV{LANGUAGE}")
  set(ENV{LANG} "C")
  set(ENV{LC_ALL} "C")
  set(ENV{LANGUAGE} "C")
  # A successful first MSYS2 startup can still emit GPG and hardlink
  # diagnostics containing the word "error" on stderr. Visual Studio's custom
  # build parser treats those lines as build errors even when pacman returns 0.
  execute_process(
    COMMAND ${ARGV}
    RESULT_VARIABLE result
    ERROR_VARIABLE stderr_output
  )
  set(ENV{LANG} "${old_lang}")
  set(ENV{LC_ALL} "${old_lc_all}")
  set(ENV{LANGUAGE} "${old_language}")
  if(NOT "${result}" STREQUAL "0")
    string(REPLACE ";" " " rendered "${ARGV}")
    message(FATAL_ERROR "Command failed (${result}): ${rendered}\n${stderr_output}")
  endif()
  if(NOT stderr_output STREQUAL "")
    message(STATUS "MSYS2 package installation emitted non-fatal stderr diagnostics; continuing.")
  endif()
endfunction()

function(command_line out_var)
  string(REPLACE ";" " " rendered "${ARGN}")
  set(${out_var} "${rendered}" PARENT_SCOPE)
endfunction()

function(run_bundler_install ruby_exe gemfile_path out_var)
  execute_process(
    COMMAND "${ruby_exe}" -S bundle install --gemfile "${gemfile_path}"
    RESULT_VARIABLE result
  )
  set(${out_var} "${result}" PARENT_SCOPE)
endfunction()

function(ensure_bundler ruby_exe)
  execute_process(
    COMMAND "${ruby_exe}" -S gem install bundler --no-document
    RESULT_VARIABLE result
  )
  if(NOT "${result}" STREQUAL "0")
    execute_process(
      COMMAND "${ruby_exe}" -S bundle --version
      RESULT_VARIABLE bundle_result
      OUTPUT_QUIET
      ERROR_QUIET
    )
    if("${bundle_result}" STREQUAL "0")
      message(WARNING "gem install bundler returned ${result}, but bundle is available; continuing.")
    else()
      command_line(rendered "${ruby_exe}" -S gem install bundler --no-document)
      message(FATAL_ERROR "Command failed (${result}): ${rendered}")
    endif()
  endif()
endfunction()

function(bundle_check_gems ruby_exe gemfile_path out_var)
  execute_process(
    COMMAND "${ruby_exe}" -S bundle check --gemfile "${gemfile_path}"
    RESULT_VARIABLE result
    OUTPUT_QUIET
    ERROR_QUIET
  )
  if("${result}" STREQUAL "0")
    set(${out_var} ON PARENT_SCOPE)
  else()
    set(${out_var} OFF PARENT_SCOPE)
  endif()
endfunction()

function(ensure_bundle_lockfile ruby_exe gemfile_path runtime_lockfile)
  if(NOT EXISTS "${runtime_lockfile}")
    message(STATUS "Generating platform Gemfile.lock at ${runtime_lockfile}")
    execute_process(
      COMMAND "${ruby_exe}" -S bundle lock --gemfile "${gemfile_path}"
      RESULT_VARIABLE result
    )
    if(NOT "${result}" STREQUAL "0")
      command_line(rendered "${ruby_exe}" -S bundle lock --gemfile "${gemfile_path}")
      message(FATAL_ERROR "Command failed (${result}): ${rendered}")
    endif()
  endif()
  if(NOT EXISTS "${runtime_lockfile}")
    message(FATAL_ERROR "Bundler did not produce expected Gemfile.lock: ${runtime_lockfile}")
  endif()
endfunction()

function(copy_directory_contents source destination)
  file(MAKE_DIRECTORY "${destination}")
  if(PLATFORM STREQUAL "windows")
    file(TO_NATIVE_PATH "${source}" source_native)
    file(TO_NATIVE_PATH "${destination}" destination_native)
    set(robocopy_executable "$ENV{SystemRoot}/System32/robocopy.exe")
    if(NOT EXISTS "${robocopy_executable}")
      set(robocopy_executable robocopy)
    endif()
    execute_process(
      COMMAND "${robocopy_executable}" "${source_native}" "${destination_native}" /E /NFL /NDL /NJH /NJS /NP
      RESULT_VARIABLE result
    )
    if(NOT result MATCHES "^[0-9]+$")
      message(FATAL_ERROR "Robocopy failed to start: ${result}")
    endif()
    if(result GREATER_EQUAL 8)
      message(FATAL_ERROR "Robocopy failed (${result}): ${source} -> ${destination}")
    endif()
  else()
    file(COPY "${source}/" DESTINATION "${destination}")
  endif()
endfunction()

function(runtime_ruby_path out_var)
  if(PLATFORM STREQUAL "windows")
    set(candidate "${RUNTIME_ROOT}/bin/ruby.exe")
  else()
    set(candidate "${RUNTIME_ROOT}/bin/ruby")
  endif()
  set(${out_var} "${candidate}" PARENT_SCOPE)
endfunction()

function(download_if_needed url destination label)
  if(EXISTS "${destination}")
    file(SIZE "${destination}" downloaded_size)
    if(downloaded_size EQUAL 0)
      file(REMOVE "${destination}")
    endif()
  endif()
  if(NOT EXISTS "${destination}")
    message(STATUS "Downloading ${label}: ${url}")
    file(DOWNLOAD
      "${url}"
      "${destination}"
      SHOW_PROGRESS
      STATUS download_status
      LOG download_log
      TLS_VERIFY ON
    )
    list(GET download_status 0 download_code)
    if(NOT download_code EQUAL 0)
      list(GET download_status 1 download_message)
      message(FATAL_ERROR "${label} download failed: ${download_message}\n${download_log}")
    endif()
    file(SIZE "${destination}" downloaded_size)
    if(downloaded_size EQUAL 0)
      file(REMOVE "${destination}")
      message(FATAL_ERROR "${label} download produced an empty file: ${url}")
    endif()
  endif()
endfunction()

function(prepare_seven_zip out_var)
  if(NOT SEVEN_ZIP_URL)
    message(FATAL_ERROR "SEVEN_ZIP_URL is required for extracting Windows self-extracting archives")
  endif()
  if(NOT SEVEN_ZIP_EXTRA_URL)
    message(FATAL_ERROR "SEVEN_ZIP_EXTRA_URL is required for extracting Windows self-extracting archives")
  endif()
  set(tools_dir "${RUNTIME_ROOT}/../tools")
  file(MAKE_DIRECTORY "${tools_dir}")
  set(seven_zr_path "${tools_dir}/7zr.exe")
  set(seven_za_path "${tools_dir}/7za.exe")
  download_if_needed("${SEVEN_ZIP_URL}" "${seven_zr_path}" "7-Zip bootstrap")

  if(NOT EXISTS "${seven_za_path}")
    get_filename_component(seven_zip_extra_name "${SEVEN_ZIP_EXTRA_URL}" NAME)
    if(NOT seven_zip_extra_name)
      set(seven_zip_extra_name "7z-extra.7z")
    endif()
    set(seven_zip_extra_path "${tools_dir}/${seven_zip_extra_name}")
    set(seven_zip_extra_extract_root "${tools_dir}/7z-extra")
    download_if_needed("${SEVEN_ZIP_EXTRA_URL}" "${seven_zip_extra_path}" "7-Zip extra")
    file(REMOVE_RECURSE "${seven_zip_extra_extract_root}")
    file(MAKE_DIRECTORY "${seven_zip_extra_extract_root}")
    run_checked(
      "${seven_zr_path}"
      x
      "-o${seven_zip_extra_extract_root}"
      -y
      "${seven_zip_extra_path}"
    )
    file(GLOB_RECURSE seven_za_candidates "${seven_zip_extra_extract_root}/7za.exe")
    list(LENGTH seven_za_candidates seven_za_count)
    if(seven_za_count EQUAL 0)
      message(FATAL_ERROR "7-Zip extra archive does not contain 7za.exe")
    endif()
    list(GET seven_za_candidates 0 seven_za_candidate)
    file(COPY_FILE "${seven_za_candidate}" "${seven_za_path}" ONLY_IF_DIFFERENT)
    file(REMOVE_RECURSE "${seven_zip_extra_extract_root}")
  endif()
  set(${out_var} "${seven_za_path}" PARENT_SCOPE)
endfunction()

function(extract_cached_msys2_package package_glob)
  file(GLOB packages "${msys_root}/var/cache/pacman/pkg/${package_glob}")
  list(LENGTH packages package_count)
  if(package_count EQUAL 0)
    message(FATAL_ERROR "MSYS2 package cache does not contain ${package_glob}")
  endif()
  list(SORT packages)
  list(GET packages -1 package_path)
  prepare_seven_zip(seven_zip)

  set(package_extract_root "${RUNTIME_ROOT}/../package-extract-${ARCH}")
  file(REMOVE_RECURSE "${package_extract_root}")
  file(MAKE_DIRECTORY "${package_extract_root}")
  run_checked(
    "${seven_zip}"
    x
    "-o${package_extract_root}"
    -y
    "${package_path}"
  )
  file(GLOB_RECURSE package_tars "${package_extract_root}/*.tar")
  list(LENGTH package_tars package_tar_count)
  if(package_tar_count EQUAL 0)
    message(FATAL_ERROR "MSYS2 package extraction did not produce a tar payload: ${package_path}")
  endif()
  foreach(package_tar IN LISTS package_tars)
    execute_process(
      COMMAND
        "${seven_zip}"
        x
        "-o${msys_root}"
        -y
        "${package_tar}"
      RESULT_VARIABLE package_extract_result
      OUTPUT_QUIET
      ERROR_QUIET
    )
    if(NOT package_extract_result EQUAL 0)
      message(STATUS "MSYS2 package extraction reported hardlink errors; continuing with copied tool aliases")
    endif()
  endforeach()
  file(REMOVE_RECURSE "${package_extract_root}")
endfunction()

function(copy_file_if_missing source destination)
  if(NOT EXISTS "${destination}" AND EXISTS "${source}")
    get_filename_component(destination_dir "${destination}" DIRECTORY)
    file(MAKE_DIRECTORY "${destination_dir}")
    file(COPY_FILE "${source}" "${destination}")
  endif()
endfunction()

function(repair_msys2_toolchain_links)
  if(ARCH STREQUAL "x64")
    set(mingw_prefix "ucrt64")
    set(package_prefix "mingw-w64-ucrt-x86_64")
    set(target_triplet "x86_64-w64-mingw32")
  elseif(ARCH STREQUAL "x86")
    set(mingw_prefix "mingw32")
    set(package_prefix "mingw-w64-i686")
    set(target_triplet "i686-w64-mingw32")
  elseif(ARCH STREQUAL "arm64")
    set(mingw_prefix "clangarm64")
    set(package_prefix "mingw-w64-clang-aarch64")
    set(target_triplet "aarch64-w64-mingw32")
  else()
    message(FATAL_ERROR "Unsupported Windows MSYS2 arch: ${ARCH}")
  endif()

  set(mingw_bin "${msys_root}/${mingw_prefix}/bin")
  set(target_bin "${msys_root}/${mingw_prefix}/${target_triplet}/bin")
  if(ARCH STREQUAL "arm64")
    foreach(tool clang clang++ clang-cpp gcc g++)
      copy_file_if_missing("${mingw_bin}/cc.exe" "${mingw_bin}/${tool}.exe")
      if(NOT EXISTS "${mingw_bin}/${tool}.exe")
        message(FATAL_ERROR "MSYS2 compiler tool was not prepared: ${mingw_bin}/${tool}.exe")
      endif()
      copy_file_if_missing("${mingw_bin}/${tool}.exe" "${mingw_bin}/${target_triplet}-${tool}.exe")
    endforeach()
    copy_file_if_missing("${mingw_bin}/ld.lld.exe" "${mingw_bin}/ld.exe")
    copy_file_if_missing("${mingw_bin}/ld.exe" "${mingw_bin}/lld.exe")
  elseif(NOT EXISTS "${mingw_bin}/ld.exe")
    message(STATUS "Repairing MSYS2 binutils hardlinks for ${ARCH}")
    extract_cached_msys2_package("${package_prefix}-binutils-*.pkg.tar.zst")
  endif()

  copy_file_if_missing("${mingw_bin}/ld.bfd.exe" "${mingw_bin}/ld.exe")
  foreach(tool ar as dlltool nm objcopy objdump ranlib readelf strip windres)
    copy_file_if_missing("${mingw_bin}/${tool}.exe" "${target_bin}/${tool}.exe")
  endforeach()
  copy_file_if_missing("${mingw_bin}/ld.bfd.exe" "${target_bin}/ld.bfd.exe")
  copy_file_if_missing("${mingw_bin}/ld.exe" "${target_bin}/ld.exe")

  if(NOT EXISTS "${mingw_bin}/ld.exe")
    message(FATAL_ERROR "MSYS2 linker was not prepared: ${mingw_bin}/ld.exe")
  endif()

  if(NOT ARCH STREQUAL "arm64")
    # GCC packages use hardlinks for compiler aliases and the LTO plugin.
    # Shared filesystems may leave only the original file from each group.
    # Older packages use gcc/g++ as the originals; newer ones use cc/c++.
    copy_file_if_missing("${mingw_bin}/cc.exe" "${mingw_bin}/gcc.exe")
    copy_file_if_missing("${mingw_bin}/gcc.exe" "${mingw_bin}/cc.exe")
    copy_file_if_missing("${mingw_bin}/c++.exe" "${mingw_bin}/g++.exe")
    copy_file_if_missing("${mingw_bin}/g++.exe" "${mingw_bin}/c++.exe")
    foreach(tool cc gcc c++ g++ gcc-ar gcc-nm gcc-ranlib)
      if(NOT EXISTS "${mingw_bin}/${tool}.exe")
        message(FATAL_ERROR "MSYS2 compiler tool was not prepared: ${mingw_bin}/${tool}.exe")
      endif()
      copy_file_if_missing("${mingw_bin}/${tool}.exe" "${mingw_bin}/${target_triplet}-${tool}.exe")
    endforeach()

    file(GLOB gcc_version_dirs LIST_DIRECTORIES true
      "${msys_root}/${mingw_prefix}/lib/gcc/${target_triplet}/*")
    foreach(gcc_version_dir IN LISTS gcc_version_dirs)
      if(NOT IS_DIRECTORY "${gcc_version_dir}")
        continue()
      endif()
      get_filename_component(gcc_version "${gcc_version_dir}" NAME)
      copy_file_if_missing("${mingw_bin}/gcc.exe" "${mingw_bin}/${target_triplet}-gcc-${gcc_version}.exe")
      set(gcc_lto_plugin "${gcc_version_dir}/liblto_plugin.dll")
      set(bfd_lto_plugin "${msys_root}/${mingw_prefix}/lib/bfd-plugins/liblto_plugin.dll")
      copy_file_if_missing("${bfd_lto_plugin}" "${gcc_lto_plugin}")
      copy_file_if_missing("${gcc_lto_plugin}" "${bfd_lto_plugin}")
      if(NOT EXISTS "${gcc_lto_plugin}")
        message(FATAL_ERROR "MSYS2 GCC LTO plugin was not prepared: ${gcc_lto_plugin}")
      endif()
    endforeach()
  endif()
endfunction()

runtime_ruby_path(ruby_exe)

set(reset_runtime OFF)
if(EXISTS "${marker}")
  file(READ "${marker}" current_marker)
  if(NOT current_marker STREQUAL desired_marker)
    set(reset_runtime ON)
  endif()
endif()

if(reset_runtime)
  file(REMOVE_RECURSE "${RUNTIME_ROOT}")
endif()

if(PLATFORM STREQUAL "windows")
  if(NOT WINDOWS_RUBY_URL)
    message(FATAL_ERROR "WINDOWS_RUBY_URL is required for Windows Ruby runtime")
  endif()
  if(NOT MSYS2_URL)
    message(FATAL_ERROR "MSYS2_URL is required for Windows Ruby runtime")
  endif()

  if(NOT EXISTS "${ruby_exe}")
    file(MAKE_DIRECTORY "${RUNTIME_ROOT}")
    set(download_dir "${RUNTIME_ROOT}/../downloads")
    file(MAKE_DIRECTORY "${download_dir}")
    get_filename_component(archive_name "${WINDOWS_RUBY_URL}" NAME)
    if(NOT archive_name)
      set(archive_name "rubyinstaller-${RUBY_VERSION}-${rubyinstaller_arch}.7z")
    endif()
    set(archive_path "${download_dir}/${archive_name}")

    download_if_needed("${WINDOWS_RUBY_URL}" "${archive_path}" "RubyInstaller")

    string(TOLOWER "${archive_path}" archive_path_lower)
    if(NOT archive_path_lower MATCHES "\\.7z$")
      message(FATAL_ERROR "Windows Ruby runtime must be a .7z RubyInstaller archive: ${archive_path}")
    endif()

    file(REMOVE_RECURSE "${RUNTIME_ROOT}")
    file(MAKE_DIRECTORY "${RUNTIME_ROOT}")
    message(STATUS "Extracting RubyInstaller archive to ${RUNTIME_ROOT}")
    prepare_seven_zip(seven_zip)
    run_checked(
      "${seven_zip}"
      x
      "-o${RUNTIME_ROOT}"
      -y
      "${archive_path}"
    )

    if(NOT EXISTS "${ruby_exe}")
      file(GLOB_RECURSE ruby_candidates
        "${RUNTIME_ROOT}/ruby.exe"
        "${RUNTIME_ROOT}/bin/ruby.exe"
        "${RUNTIME_ROOT}/*/ruby.exe"
        "${RUNTIME_ROOT}/*/bin/ruby.exe"
      )
      list(LENGTH ruby_candidates ruby_candidate_count)
      if(ruby_candidate_count EQUAL 0)
        message(FATAL_ERROR "Extracted RubyInstaller does not contain ruby.exe")
      endif()
      list(GET ruby_candidates 0 nested_ruby)
      get_filename_component(nested_bin "${nested_ruby}" DIRECTORY)
      get_filename_component(nested_root "${nested_bin}" DIRECTORY)
      if(NOT nested_root STREQUAL RUNTIME_ROOT)
        file(GLOB nested_children LIST_DIRECTORIES true "${nested_root}/*")
        foreach(child IN LISTS nested_children)
          get_filename_component(child_name "${child}" NAME)
          file(RENAME "${child}" "${RUNTIME_ROOT}/${child_name}")
        endforeach()
        file(REMOVE_RECURSE "${nested_root}")
      endif()
    endif()
  endif()

  set(msys_root "${RUNTIME_ROOT}/msys64")
  set(msys_bash "${msys_root}/usr/bin/bash.exe")
  set(msys_stamp "${msys_root}/.elten-msys2.stamp")
  set(msys_marker "source=msys2-base\nurl=${MSYS2_URL}\npackages=${MSYS2_PACKAGES}\n")
  set(msys_reset OFF)
  if(EXISTS "${msys_stamp}")
    file(READ "${msys_stamp}" current_msys_marker)
    if(NOT current_msys_marker STREQUAL msys_marker)
      set(msys_reset ON)
    endif()
  endif()
  if(msys_reset)
    file(REMOVE_RECURSE "${msys_root}")
  endif()
  if(NOT EXISTS "${msys_bash}")
    set(download_dir "${RUNTIME_ROOT}/../downloads")
    file(MAKE_DIRECTORY "${download_dir}")
    get_filename_component(msys_archive_name "${MSYS2_URL}" NAME)
    if(NOT msys_archive_name)
      set(msys_archive_name "msys2-base-x86_64.tar.xz")
    endif()
    set(msys_archive_path "${download_dir}/${msys_archive_name}")
    download_if_needed("${MSYS2_URL}" "${msys_archive_path}" "MSYS2")
    message(STATUS "Extracting MSYS2 to ${RUNTIME_ROOT}")
    file(ARCHIVE_EXTRACT INPUT "${msys_archive_path}" DESTINATION "${RUNTIME_ROOT}")
  endif()
  if(NOT EXISTS "${msys_bash}")
    message(FATAL_ERROR "MSYS2 bash was not prepared: ${msys_bash}")
  endif()

  if(NOT EXISTS "${msys_stamp}")
    set(pacman_conf "${msys_root}/etc/pacman.conf")
    if(EXISTS "${pacman_conf}")
      file(READ "${pacman_conf}" pacman_conf_text)
      string(REGEX REPLACE "(^|\n)SigLevel[^\n]*" "\\1SigLevel = Never" pacman_conf_text "${pacman_conf_text}")
      string(REGEX REPLACE "(^|\n)LocalFileSigLevel[^\n]*" "\\1LocalFileSigLevel = Never" pacman_conf_text "${pacman_conf_text}")
      file(WRITE "${pacman_conf}" "${pacman_conf_text}")
    endif()
    message(STATUS "Installing MSYS2 packages: ${MSYS2_PACKAGES}")
    run_checked_with_c_locale(
      "${msys_bash}"
      -lc
      "pacman --color never --noconfirm --needed -Sy ${MSYS2_PACKAGES}"
    )
  endif()
  # Also repair runtimes installed before these checks were added.
  repair_msys2_toolchain_links()
  if(NOT EXISTS "${msys_stamp}")
    file(WRITE "${msys_stamp}" "${msys_marker}")
  endif()
elseif(PLATFORM STREQUAL "osx")
  set(osx_ruby_configure_args)
  if(OSX_RUBY_CONFIGURE_OPTIONS)
    separate_arguments(osx_ruby_configure_args NATIVE_COMMAND "${OSX_RUBY_CONFIGURE_OPTIONS}")
  endif()
  if(NOT EXISTS "${ruby_exe}")
    find_program(RUBY_INSTALL_EXECUTABLE ruby-install)
    if(NOT RUBY_INSTALL_EXECUTABLE)
      message(FATAL_ERROR "ruby-install not found. Install ruby-install and rerun the build.")
    endif()
    file(REMOVE_RECURSE "${RUNTIME_ROOT}")
    get_filename_component(runtime_parent "${RUNTIME_ROOT}" DIRECTORY)
    file(MAKE_DIRECTORY "${runtime_parent}")
    message(STATUS "Installing Ruby ${RUBY_VERSION} to ${RUNTIME_ROOT}")
    if(osx_ruby_configure_args)
      message(STATUS "macOS Ruby configure options: ${OSX_RUBY_CONFIGURE_OPTIONS}")
    endif()
    set(ruby_install_command
      "${RUBY_INSTALL_EXECUTABLE}"
      ruby
      "${RUBY_VERSION}"
      --install-dir
      "${RUNTIME_ROOT}"
    )
    if(osx_ruby_configure_args)
      list(APPEND ruby_install_command -- ${osx_ruby_configure_args})
    endif()
    run_checked(${ruby_install_command})
  endif()
elseif(PLATFORM STREQUAL "linux")
  set(linux_ruby_configure_args)
  if(LINUX_RUBY_CONFIGURE_OPTIONS)
    separate_arguments(linux_ruby_configure_args NATIVE_COMMAND "${LINUX_RUBY_CONFIGURE_OPTIONS}")
  endif()
  if(NOT EXISTS "${ruby_exe}")
    if(NOT LINUX_RUBY_URL)
      message(FATAL_ERROR "LINUX_RUBY_URL is required for Linux Ruby runtime")
    endif()
    set(download_dir "${RUNTIME_ROOT}/../downloads")
    file(MAKE_DIRECTORY "${download_dir}")
    get_filename_component(ruby_archive_name "${LINUX_RUBY_URL}" NAME)
    if(NOT ruby_archive_name)
      set(ruby_archive_name "ruby-${RUBY_VERSION}.tar.gz")
    endif()
    set(ruby_archive_path "${download_dir}/${ruby_archive_name}")
    download_if_needed("${LINUX_RUBY_URL}" "${ruby_archive_path}" "Ruby source")

    set(ruby_src_root "${RUNTIME_ROOT}/../ruby-src-${ARCH}")
    file(REMOVE_RECURSE "${ruby_src_root}")
    file(MAKE_DIRECTORY "${ruby_src_root}")
    message(STATUS "Extracting Ruby source to ${ruby_src_root}")
    file(ARCHIVE_EXTRACT INPUT "${ruby_archive_path}" DESTINATION "${ruby_src_root}")
    file(GLOB ruby_src_candidates LIST_DIRECTORIES true "${ruby_src_root}/ruby-*")
    list(LENGTH ruby_src_candidates ruby_src_count)
    if(ruby_src_count EQUAL 0)
      message(FATAL_ERROR "Ruby source archive did not contain a ruby-* directory: ${ruby_archive_path}")
    endif()
    list(GET ruby_src_candidates 0 ruby_src_dir)

    file(REMOVE_RECURSE "${RUNTIME_ROOT}")
    file(MAKE_DIRECTORY "${RUNTIME_ROOT}")
    include(ProcessorCount)
    ProcessorCount(build_jobs)
    if(build_jobs EQUAL 0)
      set(build_jobs 4)
    endif()
    message(STATUS "Building Ruby ${RUBY_VERSION} for Linux (${build_jobs} jobs); this can take several minutes")
    if(linux_ruby_configure_args)
      message(STATUS "Linux Ruby configure options: ${LINUX_RUBY_CONFIGURE_OPTIONS}")
    endif()
    # Keeps bin/ruby usable during gem installation without LD_LIBRARY_PATH.
    #
    # Note this is an absolute build-tree path and is useless in the shipped
    # package. Making the objects look next to themselves instead cannot be done
    # from here: passing -Wl,-rpath,$ORIGIN through LDFLAGS gets mangled on the
    # way, differently per sub-build (make reads $O as a variable and leaves
    # "RIGIN"; mkmf drops it entirely). bundle_linux_libs.cmake rewrites RUNPATH
    # with patchelf afterwards instead, where no shell can touch the value.
    set(old_ldflags "$ENV{LDFLAGS}")
    set(ENV{LDFLAGS} "-Wl,-rpath,${RUNTIME_ROOT}/lib $ENV{LDFLAGS}")
    run_checked_in("${ruby_src_dir}"
      "${ruby_src_dir}/configure"
      "--prefix=${RUNTIME_ROOT}"
      --enable-shared
      --disable-install-doc
      ${linux_ruby_configure_args}
    )
    set(ENV{LDFLAGS} "${old_ldflags}")
    run_checked_in("${ruby_src_dir}" make "-j${build_jobs}")
    run_checked_in("${ruby_src_dir}" make install)
    file(REMOVE_RECURSE "${ruby_src_root}")
  endif()
else()
  message(FATAL_ERROR "Unsupported Ruby runtime platform: ${PLATFORM}")
endif()

if(NOT EXISTS "${ruby_exe}")
  message(FATAL_ERROR "Ruby executable was not prepared: ${ruby_exe}")
endif()

# OpenSSL embeds the CA bundle location from the machine where Ruby was
# packaged.  That location is not valid after Elten's trimmed runtime is
# installed elsewhere, so keep a relocatable copy alongside the runtime.
# RubyInstaller keeps it below the architecture-specific Ruby API directory;
# using RUBY_API_VERSION here is important because Windows x86 stays on Ruby
# 3.4 while the other current Windows targets use Ruby 4.0.
if(PLATFORM STREQUAL "windows")
  set(ruby_ca_source "${RUNTIME_ROOT}/lib/ruby/${RUBY_API_VERSION}/etc/ssl/cert.pem")
else()
  execute_process(
    COMMAND "${ruby_exe}" -ropenssl -e "print OpenSSL::X509::DEFAULT_CERT_FILE"
    RESULT_VARIABLE ruby_ca_result
    OUTPUT_VARIABLE ruby_ca_source
    ERROR_VARIABLE ruby_ca_error
    OUTPUT_STRIP_TRAILING_WHITESPACE
  )
  if(NOT ruby_ca_result EQUAL 0)
    message(FATAL_ERROR "Cannot determine Ruby OpenSSL CA bundle (${ruby_ca_result}): ${ruby_ca_error}")
  endif()
endif()
file(TO_CMAKE_PATH "${ruby_ca_source}" ruby_ca_source)
if(ruby_ca_source STREQUAL "" OR NOT EXISTS "${ruby_ca_source}")
  message(FATAL_ERROR "Ruby OpenSSL CA bundle was not found: ${ruby_ca_source}")
endif()
file(SIZE "${ruby_ca_source}" ruby_ca_source_size)
if(ruby_ca_source_size EQUAL 0)
  message(FATAL_ERROR "Ruby OpenSSL CA bundle is empty: ${ruby_ca_source}")
endif()

set(runtime_ca_bundle "${RUNTIME_ROOT}/ssl/cert.pem")
get_filename_component(ruby_ca_source_absolute "${ruby_ca_source}" ABSOLUTE)
get_filename_component(runtime_ca_bundle_absolute "${runtime_ca_bundle}" ABSOLUTE)
if(NOT ruby_ca_source_absolute STREQUAL runtime_ca_bundle_absolute)
  file(MAKE_DIRECTORY "${RUNTIME_ROOT}/ssl")
  file(COPY_FILE "${ruby_ca_source}" "${runtime_ca_bundle}" ONLY_IF_DIFFERENT)
endif()
if(NOT EXISTS "${runtime_ca_bundle}")
  message(FATAL_ERROR "Relocatable Ruby OpenSSL CA bundle was not prepared: ${runtime_ca_bundle}")
endif()
file(SIZE "${runtime_ca_bundle}" runtime_ca_bundle_size)
if(runtime_ca_bundle_size EQUAL 0)
  message(FATAL_ERROR "Relocatable Ruby OpenSSL CA bundle is empty: ${runtime_ca_bundle}")
endif()

set(runtime_gemfile "${BUNDLE_ROOT}/Gemfile")
set(runtime_lockfile "${BUNDLE_ROOT}/Gemfile.lock")
set(gem_marker "${RUNTIME_ROOT}/.elten-gemfile-gems.version")
file(MAKE_DIRECTORY "${BUNDLE_ROOT}")
file(COPY_FILE "${PROJECT_ROOT}/Gemfile" "${runtime_gemfile}" ONLY_IF_DIFFERENT)
file(SHA256 "${PROJECT_ROOT}/Gemfile" gemfile_sha)

set(runtime_nokogiri_msys_patch "")
set(nokogiri_msys_patch_sha "")
if(PLATFORM STREQUAL "windows")
  if(NOT NOKOGIRI_MSYS_PATCH OR NOT EXISTS "${NOKOGIRI_MSYS_PATCH}")
    message(FATAL_ERROR "MiniPortile MSYS path patch was not found: ${NOKOGIRI_MSYS_PATCH}")
  endif()
  set(runtime_nokogiri_msys_patch "${BUNDLE_ROOT}/patchs/mini_portile_msys_path_patch.rb")
  get_filename_component(runtime_nokogiri_msys_patch_dir "${runtime_nokogiri_msys_patch}" DIRECTORY)
  file(MAKE_DIRECTORY "${runtime_nokogiri_msys_patch_dir}")
  file(COPY_FILE "${NOKOGIRI_MSYS_PATCH}" "${runtime_nokogiri_msys_patch}" ONLY_IF_DIFFERENT)
  file(SHA256 "${NOKOGIRI_MSYS_PATCH}" nokogiri_msys_patch_sha)
endif()

set(desired_gem_marker "platform=${PLATFORM}\narch=${ARCH}\nversion=${RUBY_VERSION}\nruby_api=${RUBY_API_VERSION}\n")
set(desired_gem_marker "${desired_gem_marker}gemfile_sha=${gemfile_sha}\n")
if(PLATFORM STREQUAL "windows")
  set(desired_gem_marker "${desired_gem_marker}url=${WINDOWS_RUBY_URL}\n")
  set(desired_gem_marker "${desired_gem_marker}msys2_url=${MSYS2_URL}\n")
  set(desired_gem_marker "${desired_gem_marker}msys2_packages=${MSYS2_PACKAGES}\n")
  set(desired_gem_marker "${desired_gem_marker}nokogiri_msys_patch=${NOKOGIRI_MSYS_PATCH}\n")
  set(desired_gem_marker "${desired_gem_marker}nokogiri_msys_patch_sha=${nokogiri_msys_patch_sha}\n")
  set(desired_gem_marker "${desired_gem_marker}nokogiri_build_options=${NOKOGIRI_BUILD_OPTIONS}\n")
elseif(PLATFORM STREQUAL "osx")
  set(desired_gem_marker "${desired_gem_marker}osx_configure_options=${OSX_RUBY_CONFIGURE_OPTIONS}\n")
elseif(PLATFORM STREQUAL "linux")
  set(desired_gem_marker "${desired_gem_marker}url=${LINUX_RUBY_URL}\n")
  set(desired_gem_marker "${desired_gem_marker}linux_configure_options=${LINUX_RUBY_CONFIGURE_OPTIONS}\n")
endif()

if(INSTALL_GEMS)
  set(final_gem_dir "${RUNTIME_ROOT}/lib/ruby/gems/${RUBY_API_VERSION}")
  set(gem_dir "${final_gem_dir}")
  set(bundle_config "${RUNTIME_ROOT}/.bundle-config")
  set(gem_staging_root "")
  if(PLATFORM STREQUAL "windows")
    file(TO_CMAKE_PATH "$ENV{TEMP}" windows_temp_dir)
    if(windows_temp_dir STREQUAL "")
      set(windows_temp_dir "${RUNTIME_ROOT}/../tmp")
    endif()
    string(MD5 gem_staging_id "${RUNTIME_ROOT}")
    set(gem_staging_root "${windows_temp_dir}/elten-ruby-gems-${gem_staging_id}")
    set(gem_dir "${gem_staging_root}/gems")
    set(bundle_config "${gem_staging_root}/bundle-config")
  endif()

  set(gems_ready OFF)
  if(EXISTS "${gem_marker}" AND EXISTS "${final_gem_dir}" AND EXISTS "${runtime_lockfile}")
    file(READ "${gem_marker}" current_gem_marker)
    if("${current_gem_marker}" STREQUAL "${desired_gem_marker}")
      set(gems_ready ON)
    endif()
  endif()

  if(gems_ready)
    message(STATUS "Gemfile gems already prepared for ${PLATFORM}-${ARCH}; skipping bundle install.")
  else()
    if(gem_staging_root)
      file(REMOVE_RECURSE "${gem_staging_root}")
    endif()
    file(MAKE_DIRECTORY "${gem_dir}" "${bundle_config}")
    set(old_gem_home "$ENV{GEM_HOME}")
    set(old_gem_path "$ENV{GEM_PATH}")
    set(old_bundle_app_config "$ENV{BUNDLE_APP_CONFIG}")
    set(old_bundle_frozen "$ENV{BUNDLE_FROZEN}")
    set(old_bundle_deployment "$ENV{BUNDLE_DEPLOYMENT}")
    set(old_bundle_build_nokogiri "$ENV{BUNDLE_BUILD__NOKOGIRI}")
    set(old_bundle_build_sqlite3 "$ENV{BUNDLE_BUILD__SQLITE3}")
    set(old_path "$ENV{PATH}")
    set(old_msys2_path "$ENV{MSYS2_PATH}")
    set(old_ri_devkit "$ENV{RI_DEVKIT}")
    set(old_msystem "$ENV{MSYSTEM}")

    set(ENV{GEM_HOME} "${gem_dir}")
    set(ENV{GEM_PATH} "${gem_dir}")
    set(ENV{BUNDLE_APP_CONFIG} "${bundle_config}")
    set(ENV{BUNDLE_FROZEN} "false")
    set(ENV{BUNDLE_DEPLOYMENT} "false")
    if(PLATFORM STREQUAL "windows")
      set(msys_root "${RUNTIME_ROOT}/msys64")
      if(ARCH STREQUAL "x64")
        set(msystem "UCRT64")
        set(mingw_dir "${msys_root}/ucrt64/bin")
      elseif(ARCH STREQUAL "x86")
        set(msystem "MINGW32")
        set(mingw_dir "${msys_root}/mingw32/bin")
      elseif(ARCH STREQUAL "arm64")
        set(msystem "CLANGARM64")
        set(mingw_dir "${msys_root}/clangarm64/bin")
      else()
        message(FATAL_ERROR "Unsupported Windows gem build arch: ${ARCH}")
      endif()
      file(TO_NATIVE_PATH "${msys_root}" msys_native)
      file(TO_NATIVE_PATH "${mingw_dir}" mingw_native)
      file(TO_NATIVE_PATH "${msys_root}/usr/bin" msys_bin_native)
      file(TO_NATIVE_PATH "${RUNTIME_ROOT}/bin" ruby_bin_native)
      file(TO_NATIVE_PATH "${gem_dir}/bin" gem_bin_native)
      set(windows_gem_path "${gem_bin_native};${ruby_bin_native};${mingw_native};${msys_bin_native}")
      set(ENV{MSYS2_PATH} "${msys_native}")
      set(ENV{RI_DEVKIT} "${msys_native}")
      set(ENV{MSYSTEM} "${msystem}")
      set(ENV{BUNDLE_BUILD__NOKOGIRI} "${NOKOGIRI_BUILD_OPTIONS}")
      set(ENV{BUNDLE_BUILD__SQLITE3} "--enable-system-libraries")
      set(ENV{PATH} "${windows_gem_path};${old_path}")
    else()
      set(ENV{PATH} "${gem_dir}/bin:${RUNTIME_ROOT}/bin:${old_path}")
    endif()
    message(STATUS "Using platform Gemfile.lock at ${runtime_lockfile}")
    set(gemfile_path "${runtime_gemfile}")
    if(PLATFORM STREQUAL "windows")
      file(TO_NATIVE_PATH "${gemfile_path}" gemfile_path)
    endif()

    if(EXISTS "${final_gem_dir}" AND EXISTS "${runtime_lockfile}")
      set(check_gem_home "$ENV{GEM_HOME}")
      set(check_gem_path "$ENV{GEM_PATH}")
      set(check_path "$ENV{PATH}")
      set(ENV{GEM_HOME} "${final_gem_dir}")
      set(ENV{GEM_PATH} "${final_gem_dir}")
      if(PLATFORM STREQUAL "windows")
        file(TO_NATIVE_PATH "${final_gem_dir}/bin" final_gem_bin_native)
        set(ENV{PATH} "${final_gem_bin_native};${ruby_bin_native};${mingw_native};${msys_bin_native};${old_path}")
      endif()
      bundle_check_gems("${ruby_exe}" "${gemfile_path}" bundle_check_ok)
      set(ENV{GEM_HOME} "${check_gem_home}")
      set(ENV{GEM_PATH} "${check_gem_path}")
      set(ENV{PATH} "${check_path}")
      if(bundle_check_ok)
        set(gems_ready ON)
        file(WRITE "${gem_marker}" "${desired_gem_marker}")
        message(STATUS "Existing Gemfile gems verified for ${PLATFORM}-${ARCH}; skipping bundle install.")
      endif()
    endif()

    if(NOT gems_ready)
      if(gem_staging_root)
        message(STATUS "Installing Gemfile gems into ${gem_dir} before copying to ${final_gem_dir}")
      else()
        message(STATUS "Installing Gemfile gems into ${gem_dir}")
      endif()
      ensure_bundler("${ruby_exe}")
      run_bundler_install("${ruby_exe}" "${gemfile_path}" bundle_install_result)
      ensure_bundle_lockfile("${ruby_exe}" "${gemfile_path}" "${runtime_lockfile}")
      bundle_check_gems("${ruby_exe}" "${gemfile_path}" installed_bundle_ok)
      if(NOT "${bundle_install_result}" STREQUAL "0")
        if(installed_bundle_ok)
          message(WARNING "bundle install returned ${bundle_install_result}, but bundle check verified the installed gems; continuing.")
        else()
          command_line(rendered "${ruby_exe}" -S bundle install --gemfile "${gemfile_path}")
          message(FATAL_ERROR "Command failed (${bundle_install_result}): ${rendered}")
        endif()
      endif()
      if(NOT installed_bundle_ok)
        message(FATAL_ERROR "bundle install finished but bundle check did not verify installed gems for ${PLATFORM}-${ARCH}")
      endif()

      if(gem_staging_root)
        file(MAKE_DIRECTORY "${final_gem_dir}")
        message(STATUS "Copying staged Gemfile gems to ${final_gem_dir}")
        copy_directory_contents("${gem_dir}" "${final_gem_dir}")
        message(STATUS "Gemfile gems copied.")
        set(ENV{GEM_HOME} "${final_gem_dir}")
        set(ENV{GEM_PATH} "${final_gem_dir}")
        if(PLATFORM STREQUAL "windows")
          file(TO_NATIVE_PATH "${final_gem_dir}/bin" final_gem_bin_native)
          set(ENV{PATH} "${final_gem_bin_native};${ruby_bin_native};${mingw_native};${msys_bin_native};${old_path}")
        endif()
        bundle_check_gems("${ruby_exe}" "${gemfile_path}" final_bundle_ok)
        if(NOT final_bundle_ok)
          message(FATAL_ERROR "Copied Gemfile gems did not verify in ${final_gem_dir}")
        endif()
      endif()
      file(WRITE "${gem_marker}" "${desired_gem_marker}")
    endif()

    set(ENV{GEM_HOME} "${old_gem_home}")
    set(ENV{GEM_PATH} "${old_gem_path}")
    set(ENV{BUNDLE_APP_CONFIG} "${old_bundle_app_config}")
    set(ENV{BUNDLE_FROZEN} "${old_bundle_frozen}")
    set(ENV{BUNDLE_DEPLOYMENT} "${old_bundle_deployment}")
    set(ENV{BUNDLE_BUILD__NOKOGIRI} "${old_bundle_build_nokogiri}")
    set(ENV{BUNDLE_BUILD__SQLITE3} "${old_bundle_build_sqlite3}")
    set(ENV{PATH} "${old_path}")
    set(ENV{MSYS2_PATH} "${old_msys2_path}")
    set(ENV{RI_DEVKIT} "${old_ri_devkit}")
    set(ENV{MSYSTEM} "${old_msystem}")
  endif()
elseif(EXISTS "${gem_marker}")
  file(REMOVE "${gem_marker}")
endif()

file(WRITE "${marker}" "${desired_marker}")
file(WRITE "${RUNTIME_STAMP}" "ok\n")
message(STATUS "Ruby runtime prepared: ${RUNTIME_STAMP}")
